dnl 
dnl $Id$
dnl
dnl R embedding of the SQLite engine.
dnl
dnl Generate a "configure" script that will install the RSQLite
dnl packge.  By default, we use embed the SQLite "amalgamation" that
dnl is distributed with RSQLite.  Only if the user asks will we link
dnl to a SQLite library already installed on the system.
dnl
dnl    USAGE:  autoconf
dnl
dnl Global variables:  
dnl    PKG_CPPFLAGS and PKG_LIBS
dnl
dnl The resulting configure script implements the following logic:
dnl
dnl 1. If both PKG_CPPFLAGS and PKG_LIBS are defined, use them and exit.
dnl
dnl 2. If the user specifies explicitly a particular SQLite
dnl    installation we use that version. This can be requested through 
dnl         --with-sqlite-dir=DIR 
dnl         --with-sqlite-inc=<include-DIR>
dnl    or   --with-sqlite-lib=<library-DIR>
dnl
dnl    In the first case, DIR is assumed to include the lib and include
dnl    subdirectories;  individual locations of these two may be 
dnl    specified independently through <include-dir> and <library-dir>,
dnl    respectively.  If we find these, we exit.
dnl
dnl 3. If the user did not specify a SQLite installation, we set a
dnl    preprocessor define to include the SQLite amalgamation.
dnl

AC_INIT(src/RS-SQLite.c)

# As suggested by BDR, I may need to use CFLAGS to compile under 64-bit
# (I can't test this), but see "Writing R Extensions"
: ${R_HOME=`R RHOME`}
if test -z "${R_HOME}"; then
   echo "could not determine R_HOME"
   exit 1
fi
CC=`"${R_HOME}/bin/R" CMD config CC`
CFLAGS=`"${R_HOME}/bin/R" CMD config CFLAGS`
AC_PROG_CPP
AC_PROG_CC

FOUND_ALL="no"

#
# if users provide both PKG_CPPFLAGS *and* PKG_LIBS, then we're done!
#
test -n "${PKG_CPPFLAGS}" -a -n "${PKG_LIBS}" && FOUND_ALL="yes"

#
# does user want a particular SQLite installation/version (possibly
# overriding system directories)?
#
AC_ARG_WITH(sqlite-dir,
            [--with-sqlite-dir=DIR    specifies an existing SQLite base dir],
            SQLITE_DIR="${with_sqlite_dir}",
            SQLITE_DIR="")
AC_ARG_WITH(sqlite-lib,
            [--with-sqlite-lib=DIR    specifies an existing SQLite lib dir],
            SQLITE_LIB="${with_sqlite_lib}",
            SQLITE_LIB="")
AC_ARG_WITH(sqlite-inc,
            [--with-sqlite-inc=DIR    specifies an existing SQLite include dir],
            SQLITE_INC="${with_sqlite_inc}",
            SQLITE_INC="")
#
# Use SQLITE_DIR (if specified) to initialize SQLITE_{INC,LIB}
# (handle the uniformative cases --with-no-sqlite or w.o. DIR values,
# and make sure the various directories actually exist).  Ignore
# with-sqlite-inc and with-sqlite-lib if with-sqlite-dir is present,
# but issue a warning.
#
if test "${FOUND_ALL}" = "no"; then
    if test -n "${SQLITE_DIR}" ; then
       test ! -d "${SQLITE_DIR}" && \
                  AC_MSG_ERROR([dir ${SQLITE_DIR} does not exist])
       test -n "${SQLITE_INC}" && \
                  AC_WARN([Using sqlite-dir and ignoring sqlite-inc])
       test -n "${SQLITE_LIB}" && \
                  AC_WARN([Using sqlite-dir and ignoring sqlite-lib])
       SQLITE_INC="${SQLITE_DIR}/include"
       SQLITE_LIB="${SQLITE_DIR}/lib"
       test ! -d "${SQLITE_INC}" && \
                  AC_MSG_ERROR([dir ${SQLITE_INC} does not exist])
       test ! -d "${SQLITE_LIB}" && \
                  AC_MSG_ERROR([dir ${SQLITE_LIB} does not exist])
       PKG_LIBS="-L${SQLITE_LIB} -lsqlite3"
       PKG_CPPFLAGS="-I${SQLITE_INC}" 
       FOUND_ALL="yes"
    else
       if test -n "${SQLITE_INC}" || test -n "${SQLITE_LIB}"; then
          ## must have both
          test -n "${SQLITE_INC}" && test -n "${SQLITE_LIB}" || \
               AC_MSG_ERROR([must specify sqlite-inc and sqlite-lib])
          ## must be dirs
          test ! -d "${SQLITE_INC}" && \
               AC_MSG_ERROR([sqlite-inc dir ${SQLITE_INC} does not exist])
          test ! -d "${SQLITE_LIB}" && \
               AC_MSG_ERROR([sqlite-lib dir ${SQLITE_LIB} does not exist])
          PKG_LIBS="-L${SQLITE_LIB} -lsqlite3"
          PKG_CPPFLAGS="-I${SQLITE_INC}" 
          FOUND_ALL="yes"
       fi
    fi
fi


if test "${FOUND_ALL}" = "no" ; then
   # Since we installed successfully, now we set PKG_* vars and exit
   SQLITE_OPTS="-DSQLITE_ENABLE_RTREE"
   SQLITE_OPTS="${SQLITE_OPTS} -DSQLITE_ENABLE_FTS3"
   SQLITE_OPTS="${SQLITE_OPTS} -DSQLITE_ENABLE_FTS3_PARENTHESIS"
   SQLITE_OPTS="${SQLITE_OPTS} -DSQLITE_SOUNDEX"
   SQLITE_OPTS="${SQLITE_OPTS} -DSQLITE_MAX_VARIABLE_NUMBER=40000"
   SQLITE_OPTS="${SQLITE_OPTS} -DSQLITE_MAX_COLUMN=30000"

   PKG_CPPFLAGS="-DRSQLITE_USE_BUNDLED_SQLITE $SQLITE_OPTS"
   PKG_LIBS=""
   AC_SEARCH_LIBS(fdatasync, [rt])
   PKG_LIBS="${PKG_LIBS} $LIBS"
   FOUND_ALL="yes"
fi

AC_SUBST(PKG_CPPFLAGS)
AC_SUBST(PKG_LIBS)

AC_OUTPUT(src/Makevars)

exit 0
